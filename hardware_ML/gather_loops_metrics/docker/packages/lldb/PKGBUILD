# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lldb
pkgver=14.0.6
pkgrel=1
pkgdesc="Next generation, high-performance debugger"
arch=('x86_64')
url="https://lldb.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('python' 'python-six')
makedepends=('cmake' 'ninja' 'swig')
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/llvm-project-$pkgver.src.tar.xz{,.sig})
sha256sums=('2ac41160b0bdc74bf72d4f9a3702f290f7c09998da6fb73281cc497f4ddbe130'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  cd llvm-project-$pkgver.src
  mkdir build
}

build() {
  cd llvm-project-$pkgver.src/build

  export CC=clang
  export CXX=clang++
  local cmake_args=(
    -G Ninja
    -DLLVM_ENABLE_PROJECTS="clang;lldb"
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_SKIP_RPATH=ON
    -DCLANG_LINK_CLANG_DYLIB=OFF
    -DLLDB_USE_SYSTEM_SIX=1
    -DLLVM_ENABLE_SPHINX=OFF
    -DLLVM_LINK_LLVM_DYLIB=OFF
    -DLLVM_USE_LINKER=lld
  )
  cmake ../llvm "${cmake_args[@]}"
  ninja lldb lldb-server llvm-symbolizer
}

package() {
  cd llvm-project-$pkgver.src/build

  DESTDIR="$pkgdir" \
  ninja install-llvm-symbolizer \
        install-lldb \
        install-lldb-argdumper \
        install-lldb-server \
        install-lldb-python-scripts \
        install-liblldb
}

# vim:set ts=2 sw=2 et:
